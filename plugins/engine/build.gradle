plugins {
    id 'java'
}

group = 'MBRound18.ImmortalEngine'
version = '0.1.0'

repositories {
    mavenCentral()
}

evaluationDependsOn(":shared:utilities")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

dependencies {
    implementation project(":shared:utilities")
    compileOnly files("${rootProject.projectDir}/data/server/Server/HytaleServer.jar")
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.lwjgl:lwjgl:3.3.3'
    implementation 'com.google.code.gson:gson:2.11.0'
    testCompileOnly files("${rootProject.projectDir}/data/server/Server/HytaleServer.jar")
    testRuntimeOnly files("${rootProject.projectDir}/data/server/Server/HytaleServer.jar")
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.3'
    runtimeOnly 'org.lwjgl:lwjgl:3.3.3:natives-linux'
    runtimeOnly 'org.lwjgl:lwjgl:3.3.3:natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl:3.3.3:natives-macos'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    dependsOn(rootProject.tasks.named("ensureServerJar"))
}

jar {
    archiveBaseName.set("ImmortalEngine")
    dependsOn(project(":shared:utilities").tasks.named("jar"))
    from({
        def utilJar = project(":shared:utilities").tasks.named("jar").get().archiveFile.get().asFile
        return zipTree(utilJar)
    })
    manifest {
        attributes(
                'Plugin-Main': 'MBRound18.ImmortalEngine.ImmortalEnginePlugin',
                'Plugin-Name': 'ImmortalEngine',
                'Plugin-Version': project.version,
                'Implementation-Title': 'ImmortalEngine',
                'Implementation-Version': project.version
        )
    }
}

processResources {
    filesMatching('plugin.properties') {
        expand(version: project.version)
    }
    filesMatching('manifest.json') {
        expand(version: project.version)
    }
}

test {
    useJUnitPlatform()
}
